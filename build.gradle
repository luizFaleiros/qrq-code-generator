plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id 'com.github.spotbugs' version '6.2.2'
}

group = 'qr.code'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.google.zxing:javase:3.3.0'
    implementation 'software.amazon.awssdk:s3:2.31.35'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.3.4'
    implementation platform('io.micrometer:micrometer-bom:1.14.6')
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.opentelemetry:opentelemetry-api'
    implementation 'io.opentelemetry:opentelemetry-sdk'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
    implementation 'io.prometheus:prometheus-metrics-exporter-httpserver'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '10.26.1'
    configFile = file("${project.rootDir}/config/checkstyle/google_checks.xml")
    sourceSets = [sourceSets.main, sourceSets.test]
    ignoreFailures = false
    maxWarnings = 0
}
jacoco {
    toolVersion = '0.8.13'
    reportsDirectory = layout.buildDirectory.dir('reports/jacoco/test/xml')
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
    
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'qr/code/generator/GeneratorApplication.class',
                'qr/code/generator/configuration/**',
                'qr/code/generator/dto/**',
                'qr/code/generator/exception/**',
                '**/dto/**',
                '**/model/**',
                '**/domain/**',
                '**/entity/**'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            includes = [
                'qr.code.generator.application.cases.*',
                'qr.code.generator.core.cases.*',
                'qr.code.generator.adapters.qrcode.QrCodeController',
                'qr.code.generator.infrastructure.gateway.*',
                'qr.code.generator.infrastructure.S3StorageAdapter'
            ]
            excludes = [
                'qr.code.generator.GeneratorApplication',
                'qr.code.generator.adapters.qrcode.dto.*',
                'qr.code.generator.infrastructure.configuration.*'
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

pmd {
    toolVersion = '7.15.0'
    ignoreFailures = false
    ruleSetFiles = files("${project.rootDir}/config/pmd/custom-pmd-ruleset.xml")
    ruleSets = []
}

tasks.spotbugsMain {
    reports.create("html") {
        required = true
        outputLocation = file("$buildDir/reports/spotbugs/spotbugs.html")
        setStylesheet("fancy-hist.xsl")
    }
}